buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha04'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
}

Properties props = new Properties()
props.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.windowmirror.android"
        resValue "string", "app_name", "Butterfly"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 7
        versionName "2.0"

        buildConfigField 'String', 'SPEECH_SUBSCRIPTION_KEY', "\"${props.speechKey}\""

        // Auth0 Configuration
        manifestPlaceholders = [auth0Domain: "${props.auth0Domain}", auth0Scheme: "https"]
        resValue "string", "com_auth0_client_id", "\"${props.auth0ClientId}\""
        resValue "string", "com_auth0_domain", "\"${props.auth0Domain}\""

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ['assets']
            jniLibs.srcDir 'libs'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':aars')
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.google.guava:guava:24.0-android'
    implementation 'com.microsoft.projectoxford:speechrecognition:1.2.2'
    implementation 'com.auth0.android:auth0:1.12.1'
}

ant.importBuild 'assets.xml'
preBuild.dependsOn(list, checksum)
clean.dependsOn(clean_assets)
